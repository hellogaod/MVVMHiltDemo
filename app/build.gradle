plugins {
    id 'com.android.application'
    id 'kotlin-android'//当前模块可以使用kotlin语言
    id 'kotlin-kapt'//当前模块可以使用kapt替代annotationProcessor
    id 'dagger.hilt.android.plugin'//当前模块使用hilt插件
    id 'androidx.navigation.safeargs.kotlin'//当前模块可以使用navigation插件
    id 'kotlin-parcelize'//当前模块Kotlin使用Parcelable序列化类
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        flavorDimensions "versionCode"

    }

    signingConfigs {
        debug {
            keyAlias 'menhu'
            keyPassword 'menhu888'
            storeFile file('../mvvm.jks')
            storePassword 'menhu888'
            v1SigningEnabled true
            v2SigningEnabled true
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }
    namespace 'com.aregyan.github'

    productFlavors {

        /*sit环境*/
        sit {
            applicationId rootProject.ext.android.applicationId
            versionCode 110
            versionName "1.1.0"
            buildConfigField "String", "ENVIRONMENT", '"sit"'
        }

        /*生成环境*/
        product {
            applicationId rootProject.ext.android.applicationId
            versionCode 100
            versionName "1.0.0"
            buildConfigField "String", "ENVIRONMENT", '"product"'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def date = new Date()
            def formattedDate = date.format('YYYYMMdd-HHmm')
            if (outputFile != null && outputFileName.endsWith('.apk')) {
                outputFileName = "app-${variant.productFlavors.get(0).name}-V${versionName}-${formattedDate}.apk"
            }
        }
    }
}

dependencies {

    testImplementation rootProject.ext.test["junit"]
    androidTestImplementation rootProject.ext.test["androidxJunit"]
    androidTestImplementation rootProject.ext.test["androidxEspresso"]

    implementation rootProject.ext.jetpack["coreKtx"]
    implementation rootProject.ext.jetpack["appcompat"]
    implementation rootProject.ext.jetpack["constraintlayout"]
    implementation rootProject.ext.jetpack["navigationFragmentKtx"]
    implementation rootProject.ext.jetpack["navigationUiKtx"]

    // Hilt
    implementation rootProject.ext.dependencies["hiltAndroid"]
    kapt rootProject.ext.dependencies["hiltCompiler"]

    // Coroutines
    implementation rootProject.ext.support["kotlinxCoroutines"]
    implementation rootProject.ext.support["kotlinxCoroutinesAndroid"]

    implementation rootProject.ext.dependencies["bottomTab"]

    implementation rootProject.ext.dependencies["gson"]

    // Leak Canary
    debugImplementation rootProject.ext.dependencies["leakcanary"]

    //Room
    implementation rootProject.ext.jetpack["roomRuntime"]
    kapt rootProject.ext.jetpack["roomCompiler"]
    implementation rootProject.ext.jetpack["roomKtx"]

    implementation project(path: ':data')
    implementation project(path: ':mvvmhabit')

    //recyclerview的databinding套装
    api(rootProject.ext.dependencies.bindingcollectionadapter) {
        exclude group: 'com.android.support'
    }
    api(rootProject.ext.dependencies["bindingcollectionadapter-recyclerview"]) {
        exclude group: 'com.android.support'
    }
    api(rootProject.ext.dependencies["bindingcollectionadapter-viewpager2"]) {
        exclude group: 'com.android.support'
    }
}